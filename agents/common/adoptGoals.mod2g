use commonKnowledge as knowledge.
order=linear.

module adoptgoals{
	%If holding the next needed block go to the dropzone.
	if bel( holdingNextBlock ) then adopt(in('DropZone')).
	if bel( ahead(N), holdingNextBlock(N) ) then adopt(at('FrontDropZone')).
			
	%Otherswise, If the next needed block is known then adopt a goal to go there and hold it.
	if bel( ahead(N), not(holdingNextBlock), not(holdingNextBlock(N)), nextNeededColor(ColorID, N), 
		block(BlockID, ColorID, Place)) then adopt(in(Place),holding(BlockID)).
	if bel( not(ahead(_)), not(holdingNextBlock), nextNeededColor(ColorID), 
		block(BlockID, ColorID, Place)) then adopt(in(Place),holding(BlockID)).
			
	%Otherwise go to a random room we haven't seen yet'.
	if bel( not(ahead(_)), not(holdingNextBlock), not(finished), 
		bagof( Place,( room(Place), not(dropZone(Place)), not(visited(Place)) ), Places ), 
		random_member(Dest, Places) ) then adopt(in(Dest)).
	if bel( ahead(N), not(holdingNextBlock), not(holdingNextBlock(N)), not(finished), 
		bagof( Place, ( room(Place), not(dropZone(Place)), not(visited(Place)) ), Places ), 
		random_member(Dest, Places) ) then adopt(in(Dest)).
}